plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.epages.restdocs-api-spec' version '0.19.2'
}

group = 'jaeyeonling'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('javaVersion', JavaVersion.VERSION_21)
}

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

dependencies {
    runtimeOnly 'com.h2database:h2'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    implementation 'org.slf4j:slf4j-api'
    implementation 'org.slf4j:jcl-over-slf4j'

    implementation 'com.mysql:mysql-connector-j'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.19.2'
}

springBoot {
    mainClass = 'reactiongame.Application'
}

test {
    useJUnitPlatform()
}

openapi3 {
    servers = [
            { url = 'http://localhost:8080' },
            { url = 'http://15.164.245.42:8080' }
    ]
    title = 'Reaction Game API'
    description = 'API for Reaction Game'
    version = '0.1.0'
    format = 'yaml'
}

bootJar {
    dependsOn 'openapi3'
    from('build/api-spec/openapi3.yaml') {
        into 'static/docs'
    }
}
